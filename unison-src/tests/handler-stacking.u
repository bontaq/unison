use State get put
use Writer tell

> handle stateHandler "hello" with
  handle writerHandler [] with
  replicate 5 main

main = '(tell get)

replicate : Nat -> '{e} () -> {e} ()
replicate n x =
  if n Nat.== 0 then () else
    !x
    replicate (n `drop` 1) x

ability State a where
  get : {State a} a
  put : a -> {State a} ()

ability Writer w where
  tell : w -> {Writer w} ()

stateHandler : s -> Request {State s} a -> (s, a)
stateHandler s x = case x of
  { State.get -> k } -> handle stateHandler s with k s
  { State.put s -> k } -> handle stateHandler s with k ()
  { a } -> (s, a)

writerHandler : [w] -> Request {Writer w} a -> ([w], a)
writerHandler ww x = case x of
  { Writer.tell w -> k } -> handle writerHandler (ww `snoc` w) with k ()
  { a } -> (ww, a)
